rootProject.name = "apartment-management"

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            library('guava', 'com.google.guava:guava:32.1.3-jre')
            library('money', 'org.javamoney:moneta:1.4.2')

            version('spring-boot', '3.2.0')
            library('spring-bom', 'org.springframework.boot', 'spring-boot-dependencies').versionRef('spring-boot')

            def springStarters = [
                'spring-boot-starter-data-jpa',
                'spring-boot-starter-logging'
            ]

            springStarters.forEach { name -> 
                library(name, 'org.springframework.boot', name).withoutVersion()
            }

            bundle('spring', springStarters)

            def javaFx = [
                'javafx-controls',
                'javafx-base',
                'javafx-graphics',
                'javafx-fxml'
            ]

            version('javafx', '21')

            javaFx.forEach { name ->
                library(name, 'org.openjfx', name).versionRef('javafx')
            }

            bundle('javafx', javaFx)
        }

        testLibs {
            // TODO
        }
    }
}

include('core', 'services', 'gui')
